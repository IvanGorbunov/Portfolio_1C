
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// перед записью движений необходимо свернуть табличную часть по номенклатуре
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Управленческий.Записывать = Истина;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		// регистр ОстаткиНоменклатуры Приход
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Договор = Договор;
		Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		
		// регистр Управленческий
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		
		// Дт
		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
				
		// Кт
		Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
				
		Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
				
	КонецЦикла;
		
КонецПроцедуры
